Magnitude = Object (

< aMagnitude = (
	^self subclassResponsibility
)

<= aMagnitude = (
	^(self > aMagnitude) not
)

= aMagnitude = (
	^self subclassResponsibility
)

> aMagnitude = (
	^aMagnitude < self
)

>= aMagnitude = (
	^aMagnitude <= self
)

<=> anotherObject = (
	"Return a collation order of -1, 0, or 1, indicating whether I should be collated before the receiver, am equal, or after.
	See also:  http://en.wikipedia.org/wiki/Spaceship_operator"
	self = anotherObject ifTrue: [ ^0 ].
	self < anotherObject ifTrue: [ ^-1 ].
	^1
)

between: min and: max = (
	"Answer whether the receiver is less than or equal to the argument, max,
	and greater than or equal to the argument, min."
	min <= self ifFalse: [ ^false ].
	^self <= max
)

clampHigh: highMagnitude = (
	"Answer my value, but keep it less than highMagnitude"
	^ self min: highMagnitude
)

clampLow: lowMagnitude = (
	"Answer my value, but keep it greater than lowMagnitude"
	^ self max: lowMagnitude
)

clampLow: lowMagnitude high: highMagnitude = (
	"Answer my value, but keep it between lowMagnitude and highMagnitude"
	^ self min: highMagnitude max: lowMagnitude
)

hash = (
	"Hash must be redefined whenever = is redefined."
	^self subclassResponsibility
)

inRangeOf: first and: second = (
	"Answer whether the receiver is in the range between first and second, even if first is > second."
	^ first < second
		ifTrue:
			[ self
				between: first
				and: second ]
		ifFalse:
			[ self
				between: second
				and: first ]
)

max: aMagnitude = (
	"Answer the receiver or the argument, whichever has the greater
	magnitude."
	self > aMagnitude
		ifTrue: [^self]
		ifFalse: [^aMagnitude]
)

min: aMagnitude = (
	"Answer the receiver or the argument, whichever has the lesser
	magnitude."
	self < aMagnitude
		ifTrue: [^self]
		ifFalse: [^aMagnitude]
)

min: aMin max: aMax = (
	^ (self min: aMin) max: aMax
)

putOn: aStream = (
	(aStream isBinary ifTrue: [ self asByteArray ] ifFalse: [ self asString]) putOn: aStream
)

)
