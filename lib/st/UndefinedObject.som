UndefinedObject = Object (

asString = (
	^'nil'
)

asSetElement = (
	^ SetElement withNil
)

deepCopy = (
	"Only one instance of UndefinedObject should ever be made, so answer
	with self."
)

isEmptyOrNil = (
	^ true
)

isLiteral = (
	^ true
)

isNil = (
	^true
)

notNil = (
	^false
)

ifNil: aBlock = (
	^aBlock value
)

ifNotNil: ignoredBlock = (
	^self
)

ifNil: aBlock ifNotNil: ignoredBlock = (
	^aBlock value
)

ifNotNil: ifNotNilBlock ifNil: nilBlock = (
	"If I got here, I am nil, so evaluate the block nilBlock"
	^ nilBlock value
)

printOn: aStream = (
	aStream nextPutAll: 'nil'
)

shallowCopy = (
)

----

initializedInstance = (
	^ nil
)

)
