True = Boolean (

& aBoolean = (
	"Evaluating conjunction -- could answer aBoolean since receiver is true, but let the VM quickly check the type of the argument instead."
	aBoolean ifFalse: [ ^false ].
	^true
)

| aBoolean = (
	"Evaluating disjunction (OR) -- answer true since the receiver is true, but let the VM quickly check the type of the argument first."
	aBoolean ifTrue: [ ^true ].
	^true
)

==> aBlock = (
	^aBlock value
)

and: aBlock = (
	^aBlock value
)

asBit = (
	^ 1
)

asString = (
	^'true'
)

ifFalse: falseAlternativeBlock ifTrue: trueAlternativeBlock = (
	^trueAlternativeBlock value
)

ifFalse: ignoredBlock = (
	^nil
)

ifTrue: aBlock = (
	^aBlock value
)

ifTrue: trueAlternativeBlock ifFalse: falseAlternativeBlock = (
	^trueAlternativeBlock value
)

not = (
	^false
)

or: ignoredBlock = (
	^true
)

printOn: aStream = (
	aStream nextPutAll: 'true'
)

xor: aBoolean = (
	"aBoolean is either a boolean, or an object who's #value returns a boolean (usually a block)."
	^aBoolean value ifTrue: [ false ] ifFalse: [ true ]
)

----

initializedInstance = (
	^ nil
)

)
