CharacterSet = Set (

asString = (
	"Convert the receiver into a String"
	^String new: self size streamContents:[:s|
		self do:[:ch| s nextPut: ch].
	].
)

includesCode: anInteger = (
	^self includes: anInteger asCharacter
)

----------------------------

| Ascii CrLf Separators | "Write-once (cache)."

ascii = (
	"Return a set containing all the ASCII characters."
	^Ascii ifNil: [ Ascii := self newFrom: ((1 to: 127) collect: [:code | code asCharacter]) ]
)

crlf = (
	"Return a set containing the carriage return and line feed characters."
	^CrLf ifNil: [ CrLf := self with: Character cr with: Character lf ]
)

empty = (
 	"Return an empty set of characters"
	^self new
)

separators = (
	"Return a set containing just the whitespace characters."
	^Separators ifNil: [ Separators := self newFrom: Character separators ]
)

comment = (
	^'A set of characters.'
)

)
