Symbol = String (

asString = (
	| newString |
	newString := self species new: self size.
	newString replaceFrom: 1 to: newString size with: self startingAt: 1.
	^newString
)

asSymbol = (
)

classPart = (
	"I must be a compound selector.  Return my class name"
	| i |
	i := self indexOf: $. ifAbsent: [self error: 'class part not found'].
	^(self copyFrom: 1 to: i-1) asSymbol
)

isCompound = (
	"return true if the receiver is of the form Class.foo "
	^self includes: $.
)

isInfix = (
	"Answer whether the receiver is an infix message selector."
	^(self at: 1) isLetter not
)

isKeyword = (
	"Answer whether the receiver is a message keyword, i.e., ends with colon."
	self size <= 1 ifTrue: [^false].
	^(self at: self size) = $:
)

isSymbol = (
	^ true
)

isTestSelector = (
	^ self beginsWith: 'test'
)

keywords = (
	"Answer an array of the keywords that compose the receiver."
	| result aStream i l char |
	result := WriteStream on: (Array new: 10).
	aStream := WriteStream on: (String new: 16).
	i := 1.
	l := self size.
	[i <= l]
		whileTrue:
			[char := self at: i.
			aStream nextPut: char.
			(char = $: or: [i = l])
				ifTrue:
					[result nextPut: aStream contents.
					aStream reset].
			i := i + 1].
	^result contents
)

numArgs = (
	"Answer the number of arguments that the receiver requires if it is interpreted
	as a message selector."
	| len n i |
	len := self size.
	n := (self at: 1) isLetter ifTrue: [0] ifFalse: [1].
	i := 1.
	[(i := i + 1) <= len]
		whileTrue: "count colons"
			[(self at: i) = $: ifTrue: [n := n + 1]].
	^n
)

replaceFrom: start to: stop with: replacement startingAt: repStart = (
	self errorNoModification
)

selectorPart = (
	"return just my part after the class name if the receiver is a compound selector
	 (otherwise the whole thing)"
	^(self copyFrom: (self indexOf: $.) + 1 to: self size) asSymbol
)

shallowCopy = (
)

species = (
	"Answer the preferred class for reconstructing the receiver."
	^String
)

----

newFrom: aCollection = (
	"Answer an instance of me containing the same elements as aCollection."
	^ (aCollection as: String) asSymbol
)

)
