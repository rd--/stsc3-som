Magnitude = Object (

< aMagnitude = (
	^self subclassResponsibility
)

<= aMagnitude = (
	^(self > aMagnitude) not
)

= aMagnitude = (
	^self subclassResponsibility
)

> aMagnitude = (
	^aMagnitude < self
)

>= aMagnitude = (
	^aMagnitude <= self
)

<=> anotherObject = (
	"Return a collation order of -1, 0, or 1, indicating whether I should be collated before the receiver, am equal, or after.
	See also:  http://en.wikipedia.org/wiki/Spaceship_operator"
	self = anotherObject ifTrue: [ ^0 ].
	self < anotherObject ifTrue: [ ^-1 ].
	^1
)

between: min and: max = (
	min <= self ifFalse: [ ^false ].
	^self <= max
)

clampHigh: highMagnitude = (
	^ self min: highMagnitude
)

clampLow: lowMagnitude = (
	^ self max: lowMagnitude
)

clampLow: lowMagnitude high: highMagnitude = (
	^ self min: highMagnitude max: lowMagnitude
)

hash = (
	^self subclassResponsibility
)

inRangeOf: first and: second = (
	^ first < second
		ifTrue:
			[ self
				between: first
				and: second ]
		ifFalse:
			[ self
				between: second
				and: first ]
)

max: aMagnitude = (
	self > aMagnitude
		ifTrue: [^self]
		ifFalse: [^aMagnitude]
)

min: aMagnitude = (
	self < aMagnitude
		ifTrue: [^self]
		ifFalse: [^aMagnitude]
)

min: aMin max: aMax = (
	^ (self min: aMin) max: aMax
)

)
