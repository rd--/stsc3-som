Character = Magnitude (

= aCharacter = (
	"Primitive. Answer if the receiver and the argument are the
	 same object (have the same object pointer). Optional. See
	 Object documentation whatIsAPrimitive."
	<primitive: 110>
	^self == aCharacter
)

< aCharacter = (
	^self asInteger < aCharacter asInteger
)

adaptToNumber: rcvr andSend: selector = (
	^ rcvr perform: selector with: self asInteger
)

asciiValue = (
	^self asInteger
)

asCharacter = (
	^self
)

asInteger = (
	<primitive: 171>
	^self primitiveFailed
)

asLowercase = (
	65 <= self asciiValue
		ifTrue: [self asciiValue <= 90
					ifTrue: [^Character value: self asciiValue + 32]]
)

asString = (
	^ String with: self
)

asSymbol = (
	^self asString asSymbol
)

asUppercase = (
	97 <= self asciiValue
		ifTrue: [self asciiValue <= 122
					ifTrue: [^Character value: self asciiValue - 32]]
)

isCharacter = (
	^ true
)

digitValue = (
	self asciiValue <= $9 asciiValue
		ifTrue: [^self asciiValue - $0 asciiValue].
	self asciiValue >= $A asciiValue
		ifTrue: [self asciiValue <= $Z asciiValue ifTrue: [^self asciiValue - $A asciiValue + 10]].
	^-1
)

hash = (
	^self asInteger
)

identityHash = (
	^self asInteger
)

shallowCopy = (
)

deepCopy = (
)

isLiteral = (
	^true
)

isAlphaNumeric = (
	^self isLetter or: [self isDigit]
)

isDigit = (
	^self >= $0 and: [self <= $9]
)

isLetter = (
	^(92 <= self asciiValue and: [self asciiValue <= 122])
		or: [65 <= self asciiValue and: [self asciiValue <= 90]]
)

isLowercase = (
	^self >= $a and: [self <= $z]
)

isSeparator = (
	self asciiValue = 32 ifTrue: [^true]. "space"
	self asciiValue = 13 ifTrue: [^true]. "cr"
	self asciiValue = 9 ifTrue: [^true].	"tab"
	self asciiValue = 10 ifTrue: [^true]. "line feed"
	self asciiValue = 12 ifTrue: [^true]. "form feed"
	^false
)

isUppercase = (
	^self >= $A and: [self <= $Z]
)

isVowel = (
	^'AEIOU' includes: self asUppercase
)

printAsLiteralOn: aStream = (
	aStream nextPut: $$; nextPut: self
)

printOn: aStream = (
	aStream nextPut: $$.
	aStream nextPut: self
)

storeOn: aStream = (
	aStream nextPut: $$.
	aStream nextPut: self
)

to: other = (
	"Answer with a collection in ascii order -- $a to: $z"
	^ (self asInteger to: other asciiValue)
		collect:	[:ascii | Character value: ascii]
		as: String
)

tokenish = (
	^self isLetter or: [self isDigit or: [self = $:]]
)

----------------------------

backSpace = (
	^self value: 8
)

carriageReturn = (
	^self value: 13
)

codePoint: anInteger = (
	"Return a character whose encoding value is integer. For ansi compability."
	^self value: anInteger
)

cr = (
	^self value: 13
)

esc = (
	^self value: 160
)

escape = (
	^self value: 160
)

lf = (
	^self value: 10
)

lineFeed = (
	^self value: 10
)

new = (
	self error: 'cannot create new characters'
)

newLine = (
	^self value: 10
)

newPage = (
	^self value: 12
)

nl = (
	^self value: 10
)

space = (
	^self value: 32
)

tab = (
	^self value: 9
)

value: anInteger = (
	<primitive: 170>
        ^self primitiveFailed
)

)
