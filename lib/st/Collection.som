Collection = (

, aCollection = (
	^ self copy addAll: aCollection asCollection; yourself
)

* arg = (
	^ arg adaptToCollection: self andSend: #*
)

+ arg = (
	^ arg adaptToCollection: self andSend: #+
)

- arg = (
	^ arg adaptToCollection: self andSend: #-
)

/ arg = (
	^ arg adaptToCollection: self andSend: #/
)

adaptToCollection: rcvr andSend: selector = (
	rcvr isSequenceable & self isSequenceable ifFalse:
		[self error: 'Only sequenceable collections may be combined arithmetically'].
	^ rcvr with: self collect:
		[:rcvrElement :myElement | rcvrElement perform: selector with: myElement]
)

adaptToNumber: rcvr andSend: selector = (
	^ self collect: [:element | rcvr perform: selector with: element]
)

addAll: aCollection = (
	aCollection do: [:each | self add: each].
	^ aCollection
)

addIfNotPresent: anObject = (
	"Include anObject as one of the receiver's elements, but only if there
	is no such element already. Anwser anObject."
	(self includes: anObject) ifFalse: [self add: anObject].
	^ anObject
)

allSatisfy: aBlock = (
	"Evaluate aBlock with the elements of the receiver.
	If aBlock returns false for any element return false.
	Otherwise return true."
	self do: [:each | (aBlock value: each) ifFalse: [^ false]].
	^ true
)

any: numberOfElements = (
	^ self any: numberOfElements as: self species
)

any: numberOfElements as: aClass = (
	"Enumerate this colleciton and return the specified number of elements. Signals an error if this collection has not enough elements."
	| index result |
	index := 0.
	result := aClass new: numberOfElements.
	result fillFrom: self with: [:each |
		(index := index + 1) > numberOfElements
			ifTrue: [^ result]
			ifFalse: [each]].
	index = numberOfElements
		ifFalse: [self error: 'Not enough elements in this collection.'].
	^ result
)

anyOne = (
	self do: [:each | ^ each].
	self errorEmptyCollection
)

anySatisfy: aBlock = (
	self do: [:each | (aBlock value: each) ifTrue: [^ true]].
	^ false
)

asArray = (
	| array index |
	array := Array new: self size.
	index := 0.
	self do: [:each | array at: (index := index + 1) put: each].
	^ array
)

asBag = (
	"Answer a Bag whose elements are the elements of the receiver."
	^ Bag withAll: self
)

asCollection = (
	^ self
)

asIdentitySet = (
	^(IdentitySet new: self size) addAll: self; yourself
)

asOrderedCollection = (
	"Answer an OrderedCollection whose elements are the elements of the
	receiver. The order in which elements are added depends on the order
	in which the receiver enumerates its elements. In the case of unordered
	collections, the ordering is not necessarily the same for multiple
	requests for the conversion."
	^ self as: OrderedCollection
)

asSet = (
	^ Set withAll: self
)

asSortedArray = (
	"Return a copy of the receiver in sorted order, as an Array.  6/10/96 sw"
	^ self asSortedCollection asArray
)

asSortedCollection = (
	"Answer a SortedCollection whose elements are the elements of the
	receiver. The sort order is the default less than or equal.
	Use #sorted: if you don't really need a SortedCollection, but a sorted collection!"
	^self as: SortedCollection
)

asSortedCollection: aSortBlock = (
	"Answer a SortedCollection whose elements are the elements of the
	receiver. The sort order is defined by the argument, aSortBlock.
	Use #sorted: if you don't really need a SortedCollection, but a sorted collection!"
	| aSortedCollection |
	aSortedCollection := SortedCollection new: self size.
	aSortedCollection sortBlock: aSortBlock.
	aSortedCollection addAll: self.
	^ aSortedCollection
)

associationsDo: aBlock = (
	"Evaluate aBlock for each of the receiver's elements (key/value
	associations).  If any non-association is within, the error is not caught now,
	but later, when a key or value message is sent to it."
	self do: aBlock
)

atRandom = (
	"Answer a random element of the receiver.  Uses the process-local random number generator. Causes an error if self has no elements."
	^self atRandom: Random
)

atRandom: aGenerator = (
	"Answer a random element of the receiver. Uses aGenerator which
	should be kept by the user in a variable and used every time. Use
	this instead of #atRandom for better uniformity of random numbers because
	only you use the generator. Causes an error if self has no elements."
	| randomIndex index |
	self emptyCheck.
	randomIndex := aGenerator nextInt: self size.
	index := 1.
	self do: [ :each |
		index = randomIndex ifTrue: [ ^each ].
		index := index + 1 ]
)

average = (
	^ self sum / self size
)

ceiling = (
	^ self collect: [:a | a ceiling]
)

collect: aBlock = (
	| newCollection |
	newCollection := self species new.
	self do: [:each | newCollection add: (aBlock value: each)].
	^ newCollection
)

collect: aBlock as: aClass = (
	"Evaluate aBlock with each of the receiver's elements as the argument.
	Collect the resulting values into an instance of aClass. Answer the resulting collection."
	^(aClass new: self size) fillFrom: self with: aBlock
)

collect: collectBlock thenDo: doBlock = (
	"Utility method to improve readability."
	^ (self collect: collectBlock) do: doBlock
)

collect: collectBlock thenSelect: selectBlock = (
	"Utility method to improve readability."
	^ (self collect: collectBlock) select: selectBlock
)

contains: aBlock = (
	^self anySatisfy: aBlock
)

copyWithout: oldElement = (
	"Answer a copy of the receiver that does not contain any
	elements equal to oldElement."
	^ self reject: [:each | each = oldElement]
)

copyWithoutAll: aCollection = (
	"Answer a copy of the receiver that does not contain any elements
	equal to those in aCollection."
	^ self reject: [:each | aCollection includes: each]
)

count: aBlock = (
	"Evaluate aBlock with each of the receiver's elements as the argument.
	Answer the number of elements that answered true."
	| sum |
	sum := 0.
	self do: [:each | (aBlock value: each) ifTrue: [sum := sum + 1]].
	^ sum
)

degreeCos = (
	^self collect: [:each | each degreeCos]
)

degreeSin = (
	^self collect: [:each | each degreeSin]
)

detect: aBlock = (
	^ self
		detect: aBlock
		ifFound: [:element | element]
		ifNone: [self errorNotFound: aBlock]
)

detect: aBlock ifFound: foundBlock = (
	^ self
		detect: aBlock
		ifFound: foundBlock
		ifNone: nil
)

detect: aBlock ifFound: foundBlock ifNone: exceptionBlock = (
	self do: [:each | (aBlock value: each) ifTrue: [^ foundBlock value: each]].
	^ exceptionBlock value
)

detect: aBlock ifNone: exceptionBlock = (
	^ self
		detect: aBlock
		ifFound: [:element | element]
		ifNone: exceptionBlock
)

detectSum: aBlock = (
	"Evaluate aBlock with each of the receiver's elements as the argument.
	Return the sum of the answers."
	| sum |
	sum := 0.
	self do: [:each |
		sum := (aBlock value: each) + sum].
	^ sum
)

difference: aCollection = (
	^ self reject: [:each | aCollection includes: each]
)

do: aBlock = (
	self subclassResponsibility
)

do: elementBlock separatedBy: separatorBlock = (
	| beforeFirst |
	beforeFirst := true.
	self do:
		[:each |
		beforeFirst
			ifTrue: [beforeFirst := false]
			ifFalse: [separatorBlock value].
		elementBlock value: each]
)

emptyCheck = (
	self isEmpty ifTrue: [self errorEmptyCollection]
)

errorDifferentSize = (
	self error: 'otherCollection must be the same size'
)

errorEmptyCollection = (
	self error: 'this collection is empty'
)

errorKeyNotFound: key = (
	"Signal KeyNotFound error"
	^(KeyNotFound key: key) signal
)

errorNotFound: anObject = (
	{self . anObject} error: 'anObject was not found in this collection'
)

fillFrom: aCollection with: aBlock = (
	"Evaluate aBlock with each of aCollections's elements as the argument.
	Collect the resulting values into self. Answer self."
	aCollection do: [ :each |
		self add: (aBlock value: each) ]
)

floor = (
	^ self collect: [:a | a floor]
)

histogramOf: aBlock = (
	^ self collect: aBlock as: Bag
)

identityIncludes: anObject = (
	"Answer whether anObject is one of the receiver's elements."
	self do: [:each | anObject == each ifTrue: [^true]].
	^false
)

ifEmpty: aBlock = (
	"Evaluate aBlock if I'm empty, return myself otherwise."
	self isEmpty ifTrue: [^ aBlock value].
)

ifEmpty: emptyBlock ifNotEmpty: notEmptyBlock = (
	"Evaluate emptyBlock if I'm empty, notEmptyBlock otherwise. If the notEmptyBlock has an argument, evalualte it with myself as its argument. See also #ifEmpty:ifNotEmptyDo:."
	self isEmpty ifTrue: [^ emptyBlock value].
	^ notEmptyBlock cull: self
)

ifEmpty: emptyBlock ifNotEmptyDo: notEmptyBlock = (
	"Evaluate emptyBlock if I'm empty, notEmptyBlock otherwise"
	"Evaluate the notEmptyBlock with the receiver as its argument"
	self isEmpty ifTrue: [ ^emptyBlock value ].
	^notEmptyBlock value: self
)

ifNotEmpty: aBlock = (
	"Evaluate aBlock if I'm not empty, return myself otherwise. If aBlock has an argument, evaluate it with myself as its argument. See also #ifNotEmptyDo:."
	self isEmpty ifFalse: [^ aBlock cull: self].
)

ifNotEmpty: notEmptyBlock ifEmpty: emptyBlock = (
	"Evaluate emptyBlock if I'm empty, notEmptyBlock otherwise. If the notEmptyBlock has an argument, evaluate it with myself as its argument. See also #ifNotEmptyDo:ifEmpty:."
	self isEmpty ifFalse: [^notEmptyBlock cull: self].
	^ emptyBlock value
)

ifNotEmptyDo: aBlock = (
	"Evaluate the given block with the receiver as its argument."
	self isEmpty ifFalse: [^ aBlock value: self].
)

ifNotEmptyDo: notEmptyBlock ifEmpty: emptyBlock = (
	"Evaluate emptyBlock if I'm empty, notEmptyBlock otherwise
	Evaluate the notEmptyBlock with the receiver as its argument"
	self isEmpty ifFalse: [ ^notEmptyBlock value: self ].
	^emptyBlock value
)

includes: anObject = (
	"Answer whether anObject is one of the receiver's elements."
	^ self anySatisfy: [:each | each = anObject]
)

includesAllOf: aCollection = (
	"Answer whether all the elements of aCollection are in the receiver."
	aCollection do: [:elem | (self includes: elem) ifFalse: [^ false]].
	^ true
)

includesAnyOf: aCollection = (
	"Answer whether any element of aCollection is one of the receiver's elements."
	aCollection do: [:elem | (self includes: elem) ifTrue: [^ true]].
	^ false
)

inject: thisValue into: binaryBlock = (
	| nextValue |
	nextValue := thisValue.
	self do: [:each | nextValue := binaryBlock value: nextValue value: each].
	^nextValue
)

intersection: aCollection = (
	^ self select: [:each | aCollection includes: each]
)

isCollection = (
	"Return true if the receiver is some sort of Collection and responds to basic collection messages such as #size and #do:"
	^true
)

isEmpty = (
	^self size = 0
)

isEmptyOrNil = (
	"Answer whether the receiver contains any elements, or is nil.  Useful in numerous situations where one wishes the same reaction to an empty collection or to nil"
	^ self isEmpty
)

isOfSameSizeCheck: otherCollection = (
	otherCollection size = self size ifFalse: [self errorDifferentSize]
)

isSequenceable = (
	^ false
)

log2 = (
	^ self collect: [:each | each log2]
)

max = (
	^ self inject: self anyOne into: [:max :each | max max: each]
)

min = (
	"Answer the minimum value in the collection.  The collection must be non-empty and contain 'compatible' Magnitudes (eg: don't try this with a collection containing both Dates and Characters)."
	^ self inject: self anyOne into: [:min :each | min min: each]
)

minMax = (
	"Scans for minimum and maximum in one pass returning the results as a two-element array"
	| min max |
	min := max := self anyOne.
	self do: [ :each |
		min := min min: each.
		max := max max: each ].
	^ Array with: min with: max
)

noneSatisfy: aBlock = (
	"Evaluate aBlock with the elements of the receiver.
	If aBlock returns false for all elements return true.
	Otherwise return false"
	self do: [:item | (aBlock value: item) ifTrue: [^ false]].
	^ true
)

notEmpty = (
	"Answer whether the receiver contains any elements."
	^ self isEmpty not
)

occurrencesOf: anObject = (
	"Answer how many of the receiver's elements are equal to anObject."
	| tally |
	tally := 0.
	self do: [:each | anObject = each ifTrue: [tally := tally + 1]].
	^tally
)

printElementsOn: aStream = (
	aStream nextPut: $(.
	self
		printElementsOn: aStream
		separatedBy: String space.
	aStream nextPut: $).
)

printElementsOn: aStream separatedBy: delimiter = (
	"Do not use #print: on the delemiter to have more control over the output. Strings get quoted, Characters get prefixed, etc."
	self
		do: [:element | aStream print: element]
		separatedBy: [aStream nextPutAll: delimiter asString].
)

printNameOn: aStream = (
	super printOn: aStream
)

product = (
    "All of my elements multiplied togeather."
    ^self inject: 1 into: [ :accumulator :item | accumulator * item ]
)

reciprocal = (
	"Return the reciever full of reciprocated elements"
	^ self collect: [:a | a reciprocal]
)

reduce: binaryBlock = (
	| first nextValue |
	first := true.
	self do: [ :each |
		first
			ifTrue: [ nextValue := each. first := false ]
			ifFalse: [ nextValue := binaryBlock value: nextValue value: each ] ].
	first ifTrue: [ self errorEmptyCollection ].
	^nextValue
)

reject: aBlock = (
	^self select: [:element | (aBlock value: element) == false]
)

reject: rejectBlock thenDo: doBlock = (
	^ (self reject: rejectBlock) do: doBlock
)

remove: oldObject = (
	^ self remove: oldObject ifAbsent: [self errorNotFound: oldObject]
)

remove: oldObject ifAbsent: anExceptionBlock = (
	self subclassResponsibility
)

removeAll: aCollection = (
	aCollection == self ifTrue: [^self removeAll].
	aCollection do: [:each | self remove: each].
	^ aCollection
)

removeAllFoundIn: aCollection = (
	"Remove each element of aCollection which is present in the receiver
	from the receiver. Answer aCollection. No error is raised if an element
	isn't found. ArrayedCollections cannot respond to this message."
	aCollection do: [:each | self remove: each ifAbsent: []].
	^ aCollection
)

roundTo: quantum= (
	^self collect: [ :ea | ea roundTo: quantum ]
)

select: aBlock = (
	| newCollection |
	newCollection := self species new.
	self do: [:each | (aBlock value: each) ifTrue: [newCollection add: each]].
	^newCollection
)

select: selectBlock thenCollect: collectBlock = (
	^ (self select: selectBlock) collect: collectBlock
)

select: selectBlock thenDo: doBlock = (
	^ (self select: selectBlock) do: doBlock
)

sign = (
	^self collect: [:each | each sign]
)

sorted = (
	"Return a new sequenceable collection which contains the same elements as self but its elements are sorted in ascending order using the #'<=' operator."
	^self sorted: nil
)

sorted: aSortBlockOrNil = (
	"Return a new sequenceable collection which contains the same elements as self but its elements are sorted by aSortBlockOrNil. The block should take two arguments and return true if the first element should preceed the second one. If aSortBlock is nil then <= is used for comparison."
	^self asArray sort: aSortBlockOrNil
)

sqrt = (
	^ self collect: [:each | each sqrt]
)

sum = (
	^self reduce: [:a :b| a + b]
)

take: maxNumberOfElements = (
	"Returns maxNumberOfElements as a new collection or less if the collection is not large enough."
	^ self any: (maxNumberOfElements min: self size)
)

truncated = (
	^ self collect: [:a | a truncated]
)

union: aCollection = (
	"Answer the set theoretic union of two collections."
	^ self asSet addAll: aCollection; yourself
)

write: anObject = (
	^ self add: anObject
)

----

isAbstract = (
	^self = Collection
)

ofSize: n = (
	^ self new: n
)

with: anObject = (
	"Answer an instance of me containing anObject."
	^ self new
		add: anObject;
		yourself
)

with: firstObject with: secondObject = (
	"Answer an instance of me containing the two arguments as elements."
	^ self new
		add: firstObject;
		add: secondObject;
		yourself
)

with: firstObject with: secondObject with: thirdObject = (
	"Answer an instance of me containing the three arguments as elements."
	^ self new
		add: firstObject;
		add: secondObject;
		add: thirdObject;
		yourself
)

with: firstObject with: secondObject with: thirdObject with: fourthObject = (
	"Answer an instance of me, containing the four arguments as the elements."
	^ self new
		add: firstObject;
		add: secondObject;
		add: thirdObject;
		add: fourthObject;
		yourself
)

with: firstObject with: secondObject with: thirdObject with: fourthObject with: fifthObject = (
	"Answer an instance of me, containing the five arguments as the elements."
	^ self new
		add: firstObject;
		add: secondObject;
		add: thirdObject;
		add: fourthObject;
		add: fifthObject;
		yourself
)

with: firstObject with: secondObject with: thirdObject with: fourthObject with: fifthObject with: sixthObject = (
	"Answer an instance of me, containing the six arguments as the elements."
	^ self new
		add: firstObject;
		add: secondObject;
		add: thirdObject;
		add: fourthObject;
		add: fifthObject;
		add: sixthObject;
		yourself
)

withAll: aCollection = (
	"Create a new collection containing all the elements from aCollection."
	^ (self new: aCollection size)
		addAll: aCollection;
		yourself
)

)
