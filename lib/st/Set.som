Set = HashedCollection (

add: newObject = (
	| index |
	index := self scanFor: newObject.
	(array at: index) ifNil: [self atNewIndex: index put: newObject asSetElement].
	^ newObject
)

addIfNotPresent: anObject = (
	^self add: anObject
)

ifAbsentAdd: anObject = (
	| index |
	index := self scanFor: anObject.
	(array at: index) ifNil:
		[self
			atNewIndex: index
			put: anObject asSetElement.
		^true].
	^false
)

= anObject = (
	self == anObject ifTrue: [ ^true ].
	self species == anObject species ifFalse: [ ^false ].
	self size = anObject size ifFalse: [ ^false ].
	^self allSatisfy: [ :each | anObject includes: each ]
)

asSet = (
	^self
)

copyWithout: oldElement = (
	| answer |
	answer := self copy	remove: oldElement ifAbsent: [].
	^answer
)

postCopy = (
	super postCopy.
	array := array copy
)

collect: aBlock = (
	| newSet |
	newSet := Set new: self size.
	self do: [ :each | newSet add: (aBlock value: each) ].
	^newSet
)

do: aBlock = (
	tally = 0 ifTrue: [ ^self ].
	1 to: array size do: [ :index |
		(array at: index) ifNotNil: [ :element |
			aBlock value: element enclosedSetElement] ]
)

occurrencesOf: anObject = (
	(self includes: anObject) ifTrue: [ ^1 ].
	^0
)

select: aBlock = (
	| newCollection |
	newCollection := self copyEmpty.
	self do: [:each | (aBlock value: each) ifTrue: [newCollection add: each]].
	^newCollection
)

remove: oldObject ifAbsent: aBlock = (
	| index |
	index := self scanFor: oldObject.
	(array at: index) ifNil: [ ^ aBlock value ].
	array at: index put: nil.
	tally := tally - 1.
	self fixCollisionsFrom: index.
	^ oldObject
)

includes: anObject = (
	(array at: (self scanFor: anObject)) ifNil: [ ^false ] ifNotNil: [ ^true ]
)

fixCollisionsFrom: start = (
	| element index |
	index := start.
	[ (element := array at: (index := index \\ array size + 1)) == nil ] whileFalse: [
		| newIndex |
		(newIndex := self scanFor: element enclosedSetElement) = index ifFalse: [
			array at: newIndex put: element.
			array at: index put: nil ] ]
)

noCheckNoGrowFillFrom: anArray = (
	1 to: anArray size do: [ :index |
		(anArray at: index) ifNotNil: [ :object |
			array
				at: (self scanForEmptySlotFor: object enclosedSetElement)
				put: object ] ]
)

remove: oldObject ifAbsent: aBlock = (
	| index |
	index := self scanFor: oldObject.
	(array at: index) ifNil: [ ^ aBlock value ].
	array at: index put: nil.
	tally := tally - 1.
	self fixCollisionsFrom: index.
	^ oldObject
)

scanFor: anObject = (
	| index start size |
	index := start := anObject hash \\ (size := array size) + 1.
	[
		| element |
		((element := array at: index) == nil or: [ anObject = element enclosedSetElement ]) ifTrue: [ ^index ].
		(index := index \\ size + 1) = start
	] whileFalse.
	self errorNoFreeSpace
)

----------------------------

newFrom: aCollection = (
	| newCollection |
	newCollection := self new: aCollection size.
	newCollection addAll: aCollection.
	^ newCollection
)

)
