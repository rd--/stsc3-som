Matrix = Collection (
| nrows ncols contents |

+* aCollection = (
	"Premultiply aCollection by self.  aCollection should be an Array or Matrix.
	 The name of this method is APL's +.x squished into Smalltalk syntax."
	^aCollection preMultiplyByMatrix: self
)

, aMatrix = (
	"Answer a new matrix having the same number of rows as the receiver and aMatrix,
	 its columns being the columns of the receiver followed by the columns of aMatrix."
	|newCont newCols anArray oldCols a b c|
	self assert: [nrows = aMatrix rowCount].
	newCont := Array new: self size + aMatrix size.
	anArray := aMatrix privateContents.
	oldCols := aMatrix columnCount.
	newCols := ncols + oldCols.
	a := b := c := 1.
	1 to: nrows do: [:r |
		newCont replaceFrom: a to: a+ncols-1 with: contents startingAt: b.
		newCont replaceFrom: a+ncols to: a+newCols-1 with: anArray startingAt: c.
		a := a + newCols.
		b := b + ncols.
		c := c + oldCols].
	^self class rows: nrows columns: newCols contents: newCont
)

,, aMatrix = (
	"Answer a new matrix having the same number of columns as the receiver and aMatrix,
	 its rows being the rows of the receiver followed by the rows of aMatrix."
	self assert: [ncols = aMatrix columnCount].
	^self class rows: nrows + aMatrix rowCount columns: ncols
		contents: contents , aMatrix privateContents
)

= aMatrix = (
	^aMatrix class == self class and: [
	 aMatrix rowCount = nrows and: [
	 aMatrix columnCount = ncols and: [
	 aMatrix privateContents = contents]]]
)

add: newObject = (
	self shouldNotImplement
)

anyOne = (
	^contents anyOne
)

asArray = (
	^contents shallowCopy
)

asBag = (
	^contents asBag
)

asByteArray = (
	^contents asByteArray
)

asCharacterSet = (
	^contents asCharacterSet
)

asFloatArray = (
	^contents asFloatArray
)

asIdentitySet = (
	^contents asIdentitySet
)

asIntegerArray = (
	^contents asIntegerArray
)

asOrderedCollection = (
	^contents asOrderedCollection
)

asSet = (
	^contents asSet
)

asSortedArray = (
	^contents asSortedArray
)

asSortedCollection = (
	^contents asSortedCollection
)

asSortedCollection: aBlock = (
	^contents asSortedCollection: aBlock
)

asWordArray = (
	^contents asWordArray
)

at: row at: column = (
	^contents at: (self indexForRow: row andColumn: column)
)

at: r at: c ifInvalid: v = (
	"If r,c is a valid index for this matrix, answer the corresponding element.
	 Otherwise, answer v."
	(r between: 1 and: nrows) ifFalse: [^v].
	(c between: 1 and: ncols) ifFalse: [^v].
	^contents at: (r-1)*ncols + c
)

at: row at: column incrementBy: value = (
	"Array2D>>at:at:add: was the origin of this method, but in Smalltalk add:
	 generally suggests adding an element to a collection, not doing a sum.
	 This method, and SequenceableCollection>>at:incrementBy: that supports
	 it, have been renamed to reveal their intention more clearly."
	^contents at: (self indexForRow: row andColumn: column) incrementBy: value
)

at: row at: column put: value = (
	^contents at: (self indexForRow: row andColumn: column) put: value
)

atAllPut: value = (
	contents atAllPut: value
)

atColumn: column = (
	|p|
	p := (self indexForRow: 1 andColumn: column)-ncols.
	^(1 to: nrows) collect: [:row | contents at: (p := p+ncols)]
)

atColumn: column put: aCollection = (
	|p|
	aCollection size = nrows ifFalse: [self error: 'wrong column size'].
	p := (self indexForRow: 1 andColumn: column)-ncols.
	aCollection do: [:each | contents at: (p := p+ncols) put: each].
	^aCollection
)

atRandom = (
	^contents atRandom
)

atRandom: aGenerator = (
	^contents atRandom: aGenerator
)

atRow: row = (
	(row between: 1 and: nrows)
		ifFalse: [self error: '1st subscript out of range'].
	^contents copyFrom: (row-1)*ncols+1 to: row*ncols
)

atRow: row put: aCollection = (
	|p|
	aCollection size = ncols ifFalse: [self error: 'wrong row size'].
	p := (self indexForRow: row andColumn: 1)-1.
	aCollection do: [:each | contents at: (p := p+1) put: each].
	^aCollection
)

atRows: rs columns: cs = (
	"Answer a Matrix obtained by slicing the receiver.
	 rs and cs should be sequenceable collections of positive integers."
	^self class rows: rs size columns: cs size tabulate: [:r :c |
		self at: (rs at: r) at: (cs at: c)]
)

atRows: r1 to: r2 columns: c1 to: c2 = (
	"Answer a submatrix [r1..r2][c1..c2] of the receiver."
	|rd cd|
	rd := r1 - 1.
	cd := c1 - 1.
	^self class rows: r2-rd columns: c2-cd tabulate: [:r :c| self at: r+rd at: c+cd]
)

atRows: r1 to: r2 columns: c1 to: c2 ifInvalid: element = (
	"Answer a submatrix [r1..r2][c1..c2] of the receiver.
	 Portions of the result outside the bounds of the original matrix
	 are filled in with element."
	|rd cd|
	rd := r1 - 1.
	cd := c1 - 1.
	^self class rows: r2-rd columns: c2-cd tabulate: [:r :c| self at: r+rd at: c+cd ifInvalid: element]
)

atRows: r1 to: r2 columns: c1 to: c2 put: aMatrix = (
	"Set the [r1..r2][c1..c2] submatrix of the receiver
	 from the [1..r2-r1+1][1..c2-c1+1] submatrix of aMatrix.
	 As long as aMatrix responds to at:at: and accepts arguments in the range shown,
	 we don't care if it is bigger or even if it is a Matrix at all."
	|rd cd|
	rd := r1 - 1.
	cd := c1 - 1.
	r1 to: r2 do: [:r |
		c1 to: c2 do: [:c |
			self at: r at: c put: (aMatrix at: r-rd at: c-cd)]].
	^aMatrix
)

collect: aBlock = (
	"Answer a new matrix with transformed elements; transformations should be independent."
	^self class rows: nrows columns: ncols contents: (contents collect: aBlock)
)

columnCount = (
	^ncols
)

diagonal = (
	"Answer (1 to: (nrows min: ncols)) collect: [:i | self at: i at: i]"
	|i|
	i := ncols negated.
	^(1 to: (nrows min: ncols)) collect: [:j | contents at: (i := i + ncols + 1)]
)

difference: aCollection = (
	"Union is in because the result is always a Set.
	 Difference and intersection are out because the result is like the receiver,
	 and with irregular seleection that cannot be."
	self shouldNotImplement
)

do: aBlock = (
	"Pass elements to aBlock one at a time in row-major order."
	contents do: aBlock
)

hash = (
	"I'm really not sure what would be a good hash function here.
	 The essential thing is that it must be compatible with #=, and
	 this satisfies that requirement."
	^contents hash
)

identityIncludes: anObject = (
	^contents identityIncludes: anObject
)

identityIndexOf: anElement = (
	^self identityIndexOf: anElement ifAbsent: [0@0]
)

identityIndexOf: anElement ifAbsent: anExceptionBlock = (
	^self rowAndColumnForIndex:
		 (contents identityIndexOf: anElement ifAbsent: [^anExceptionBlock value])
)

includes: anObject = (
	^contents includes: anObject
)

includesAllOf: aCollection = (
	^contents includesAllOf: aCollection
)

includesAnyOf: aCollection = (
	^contents includesAnyOf: aCollection
)

indexForRow: row andColumn: column = (
	(row between: 1 and: nrows)
		ifFalse: [self error: '1st subscript out of range'].
	(column between: 1 and: ncols)
		ifFalse: [self error: '2nd subscript out of range'].
	^(row-1) * ncols + column
)

indexOf: anElement = (
	"If there are integers r, c such that (self at: r at: c) = anElement,
	 answer some such r@c, otherwise answer 0@0.  This kind of perverse
	 result is provided by analogy with SequenceableCollection>>indexOf:.
	 The order in which the receiver are searched is UNSPECIFIED except
	 that it is the same as the order used by #indexOf:ifAbsent: and #readStream."
	^self indexOf: anElement ifAbsent: [0@0]
)

indexOf: anElement ifAbsent: anExceptionBlock = (
	"If there are integers r, c such that (self at: r at: c) = anElement,
	 answer some such r@c, otherwise answer the result of anExceptionBlock."
	^self rowAndColumnForIndex:
		 (contents indexOf: anElement ifAbsent: [^anExceptionBlock value])
)

indicesCollect: aBlock = (
	|r i|
	r := Array new: nrows * ncols.
	i := 0.
	1 to: nrows do: [:row |
		1 to: ncols do: [:column |
			r at: (i := i+1) put: (aBlock value: row value: column)]].
	^self class rows: nrows columns: ncols contents: r
)

indicesDo: aBlock = (
	1 to: nrows do: [:row |
		1 to: ncols do: [:column |
			aBlock value: row value: column]].
)

indicesInject: start into: aBlock = (
	|current|
	current := start.
	1 to: nrows do: [:row |
		1 to: ncols do: [:column |
			current := aBlock value: current value: row value: column]].
	^current
)

intersection: aCollection = (
	"Union is in because the result is always a Set.
	 Difference and intersection are out because the result is like the receiver,
	 and with irregular seleection that cannot be."
	self shouldNotImplement
)

isSequenceable = (
	"LIE so that arithmetic on matrices will work.
	 What matters for arithmetic is not that there should be random indexing
	 but that the structure should be stable and independent of the values of
	 the elements.  #isSequenceable is simply the wrong question to ask."
	^true
)

occurrencesOf: anObject = (
	^contents occurrencesOf: anObject
)

postCopy = (
	super postCopy.
	contents := contents copy
)

preMultiplyByArray: a = (
	"Answer a +* self where a is an Array."
	nrows = 1 ifFalse: [self error: 'dimensions do not conform'].
	^Matrix rows: a size columns: ncols tabulate: [:row :col |
		(a at: row) * (contents at: col)]
)

preMultiplyByMatrix: m = (
	"Answer m +* self where m is a Matrix."
	nrows = m columnCount ifFalse: [self error: 'dimensions do not conform'].
	^Matrix rows: m rowCount columns: ncols tabulate: [:row :col | | s |
		s := 0.
		1 to: nrows do: [:k | s := (m at: row at: k) * (self at: k at: col) + s].
		s]
)

privateContents = (
	"Only used in #, #,, and #= so far.
	 It used to be called #contents, but that clashes with Collection>>contents."
	^contents
)

readStream = (
	"Answer a ReadStream that returns all the elements of the receiver
	 in some UNSPECIFIED order."
	^ReadStream on: contents
)

reject: aBlock = (
	self shouldNotImplement
)

remove: anObject ifAbsent: anExceptionBlock = (
	self shouldNotImplement
)

removeAll = (
	self shouldNotImplement
)

replaceAll: oldObject with: newObject = (
	contents replaceAll: oldObject with: newObject
)

rowAndColumnForIndex: index = (
	|t|
	t := index - 1.
	^(t // ncols + 1)@(t \\ ncols + 1)
)

rowCount = (
	^nrows
)

rows: rows columns: columns contents: anArray = (
	self assert: [rows isInteger and: [rows >= 0]].
	self assert: [columns isInteger and: [columns >= 0]].
	self assert: [rows * columns = anArray size].
	nrows := rows.
	ncols := columns.
	contents := anArray.
	^self
)

select: aBlock = (
	self shouldNotImplement
)

shuffled = (
	^self class rows: nrows columns: ncols contents: (contents shuffled)
)

shuffledBy: aRandom = (
	^self class rows: nrows columns: ncols contents: (contents shuffledBy: aRandom)
)

size = (
	^contents size
)

storeOn: aStream = (
	aStream nextPut: $(; nextPutAll: self class name;
		nextPutAll: ' rows: '; store: nrows;
		nextPutAll: ' columns: '; store: ncols;
		nextPutAll: ' contents: '; store: contents;
		nextPut: $)
)

stringForReadout = (
	"Answer a String whose characters are a description of the receiver layed out in rows and columns"
	^ String streamContents: [:aStream |
	1
		to: self rowCount
		do: [:iRow |
			1
				to: self columnCount
				do: [:iCols |
					aStream
						print: (self at: iRow at: iCols).
					aStream tab: 2].
			aStream cr]]
)

swap: r1 at: c1 with: r2 at: c2 = (
	contents swap: (self indexForRow: r1 andColumn: c1)
			 with: (self indexForRow: r2 andColumn: c2)
)

swapColumn: anIndex withColumn: anotherIndex = (
	|a b|
	a := self indexForRow: 1 andColumn: anIndex.
	b := self indexForRow: 1 andColumn: anotherIndex.
	nrows timesRepeat: [
		contents swap: a with: b.
		a := a + ncols.
		b := b + ncols].
)

swapRow: anIndex withRow: anotherIndex = (
	|a b|
	a := self indexForRow: anIndex andColumn: 1.
	b := self indexForRow: anotherIndex andColumn: 1.
	ncols timesRepeat: [
		contents swap: a with: b.
		a := a + 1.
		b := b + 1].
)

transposed = (
	self assert: [nrows = ncols].
	^self indicesCollect: [:row :column | self at: column at: row]
)

with: aCollection collect: aBlock = (
	"aCollection must support #at:at: and be at least as large as the receiver."
	^self withIndicesCollect: [:each :row :column |
		aBlock value: each value: (aCollection at: row at: column)]
)

with: aCollection do: aBlock = (
	"aCollection must support #at:at: and be at least as large as the receiver."
	self withIndicesDo: [:each :row :column |
		aBlock value: each value: (aCollection at: row at: column)].
)

with: aCollection inject: startingValue into: aBlock = (
	"aCollection must support #at:at: and be at least as large as the receiver."
	^self withIndicesInject: startingValue into: [:value :each :row :column |
		aBlock value: value value: each value: (aCollection at: row at: column)]
)

withIndicesCollect: aBlock = (
	|i r|
	i := 0.
	r := contents shallowCopy.
	1 to: nrows do: [:row |
		1 to: ncols do: [:column |
			i := i+1.
			r at: i put: (aBlock value: (r at: i) value: row value: column)]].
	^self class rows: nrows columns: ncols contents: r
)

withIndicesDo: aBlock = (
	|i|
	i := 0.
	1 to: nrows do: [:row |
		1 to: ncols do: [:column |
			aBlock value: (contents at: (i := i+1)) value: row value: column]].
)

withIndicesInject: start into: aBlock = (
	|i current|
	i := 0.
	current := start.
	1 to: nrows do: [:row |
		1 to: ncols do: [:column |
			current := aBlock value: current value: (contents at: (i := i+1))
							  value: row value: column]].
	^current
)


----------------------------

column: aCollection = (
	"Should this be called #fromColumn:?"
	^self rows: aCollection size columns: 1 contents: aCollection asArray shallowCopy
)

diagonal: aCollection = (
	|r i|
	r := self zeros: aCollection size.
	i := 0.
	aCollection do: [:each | i := i+1. r at: i at: i put: each].
	^r
)

identity: n = (
	|r|
	r := self zeros: n.
	1 to: n do: [:i | r at: i at: i put: 1].
	^r
)

new = (
	^self rows: 0 columns: 0
)

new: dim = (
	"Answer a dim*dim matrix.  Is this an abuse of #new:?  The argument is NOT a size."
	^self rows: dim columns: dim
)

new: dim element: element = (
	"Answer a dim*dim matrix with all elements set to element.
	 Is this an abuse of #new:?  The argument is NOT a size."
	^self rows: dim columns: dim element: element
)

new: dim tabulate: aBlock = (
	"Answer a dim*dim matrix where it at: i at: j is aBlock value: i value: j."
	^self rows: dim columns: dim tabulate: aBlock
)

ones: n = (
	^self new: n element: 1
)

row: aCollection = (
	"Should this be called #fromRow:?"
	^self rows: 1 columns: aCollection size contents: aCollection asArray shallowCopy
)

rows: rows columns: columns = (
	^self rows: rows columns: columns contents: (Array new: rows*columns)
)

rows: rows columns: columns contents: contents = (
	^super new rows: rows columns: columns contents: contents
)

rows: rows columns: columns element: element = (
	^self rows: rows columns: columns
		contents: ((Array new: rows*columns) atAllPut: element; yourself)
)

rows: rows columns: columns tabulate: aBlock = (
	"Answer a new Matrix of the given dimensions where
	 result at: i at: j     is   aBlock value: i value: j"
	|a i|
	a := Array new: rows*columns.
	i := 0.
	1 to: rows do: [:row |
		1 to: columns do: [:column |
			a at: (i := i+1) put: (aBlock value: row value: column)]].
	^self rows: rows columns: columns contents: a
)

zeros: n = (
	^self new: n element: 0
)


comment = (^'
I represent a two-dimensional array, rather like Array2D.
There are three main differences between me and Array2D:
(1) Array2D inherits from ArrayedCollection, but isn''t one.  A lot of things that should work
    do not work in consequence of this.
(2) Array2D uses "at: column at: row" index order, which means that nothing you write using
    it is likely to work either.  I use the almost universal "at: row at: column" order, so it is
    much easier to adapt code from other languages without going doolally.
(3) Array2D lets you specify the class of the underlying collection, I don''t.

Structure:
  nrows : a non-negative integer saying how many rows there are.
  ncols : a non-negative integer saying how many columns there are.
  contents : an Array holding the elements in row-major order.  That is, for a 2x3 array
    the contents are (11 12 13 21 22 23).  Array2D uses column major order.

    You can specify the class of ''contents'' when you create a new Array2D,
    but Matrix always gives you an Array.

    There is a reason for this.  In strongly typed languages like Haskell and Clean,
    ''unboxed arrays'' save you both space AND time.  But in Squeak, while
    WordArray and FloatArray and so on do save space, it costs time to use them.
    A LOT of time.  I''ve measured aFloatArray sum running nearly twice as slow as
    anArray sum.  The reason is that whenever you fetch an element from an Array,
    that''s all that happens, but when you fetch an element from aFloatArray, a whole
    new Float gets allocated to hold the value.  This takes time and churns memory.
    So the paradox is that if you want fast numerical stuff, DON''T use unboxed arrays!

    Another reason for always insisting on an Array is that letting it be something
    else would make things like #, and #,, rather more complicated.  Always using Array
    is the simplest thing that could possibly work, and it works rather well.

I was trying to patch Array2D to make more things work, but just couldn''t get my head
around the subscript order.  That''s why I made Matrix.

Element-wise matrix arithmetic works; you can freely mix matrices and numbers but
don''t try to mix matrices and arrays (yet).
Matrix multiplication, using the symbol +* (derived from APL''s +.x), works between
(Matrix or Array) +* (Matrix or Array).  Don''t try to use a number as an argument of +*.
Matrix * Number and Number * Matrix work fine, so you don''t need +* with numbers.

Still to come: oodles of stuff.  Gaussian elimination maybe, other stuff probably not.

')


)
